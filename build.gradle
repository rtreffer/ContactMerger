// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
    //mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'

sourceCompatibility = 1.7

dependencies {
    implementation 'com.tundem.aboutlibraries:library:2.0.3@aar'
    implementation 'org.ow2.asm:asm:6.0'
    implementation 'com.esotericsoftware.kryo:kryo:2.24.0'
    implementation 'org.apache.lucene:lucene-core:4.7.1'
    implementation 'org.apache.lucene:lucene-misc:4.7.1'
    implementation 'org.apache.lucene:lucene-queries:4.7.1'
    implementation 'org.apache.lucene:lucene-analyzers-common:4.7.1'
    implementation 'org.apache.lucene:lucene-codecs:4.7.1'
    implementation 'com.android.support:support-v4:28.0.0'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    dexOptions {
        preDexLibraries = false
    }

    packagingOptions {
        pickFirst 'org/apache/lucene/util/SPIClassIterator.class'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/org.apache.lucene.codecs.Codec'
        exclude 'META-INF/services/org.apache.lucene.codecs.PostingsFormat'
        exclude 'META-INF/services/org.apache.lucene.codecs.DocValuesFormat'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src', 'aidl']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    signingConfigs { release { } }


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword') &&
        project.hasProperty('keyAlias')) {

        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
        android.signingConfigs.release.keyAlias = keyAlias
    } else {
        buildTypes.release.signingConfig = null
    }

}

